#RetroDevStudio.MetaData.BASIC:2049,BASIC V2
10 REM **** KELLERABEND THE GAME ***
20 GOSUB 63000
25 GOTO 355
49 REM **** EINGABE PAUSE ****
50 GET T$:IF T$="" THEN 50
51 IF T$=" " THEN RETURN
55 RETURN
60 REM ***********************
355 GOSUB 50
356 ?CHR$(144):?CHR$(147)


500 AR=1:DM=12:GS=14:LE=9:RA=12:TK=2:GG=0:DIM D%(5), WT$(4)
501 DIM RN$(12,2),RT$(12,13),ST$(16,7),ST%(16,2),ER(12,5),GS%(14,4),GS$(14,2)
503 GOSUB 10000
505 R=VAL(RN$(AR,1)):? CHR$(147)
510 GOSUB 33950

610 FORI=0TOR:?RT$(AR,I);:NEXT
666 PRINT "FREE MEM" : PRINT FRE(0)
700 REM SICHTBARE GEGENSTAENDE ANZEIGEN

710 G$="": FOR I=1 TO GS
715 IF GS%(I,1)=2 AND GS%(I,0)=AR THEN G$=G$+GS$(I,0)+" "
720 NEXT I
725 IF LEN(G$)>0 THEN PRINT"ER SIEHT: ";
730 PRINT G$;

1100 IF LEN(G$)=0 THEN PRINT "";
1105 PRINT "ANDERE ORTE:";
1110 FOR I=1 TO ER(AR,0):? RN$(ER(AR,I),0);ER(AR,I);:NEXT
1200 FOR I=1 TO WD:WD$(I)="":NEXT:WD=0
1210 ? CHR$(13) CHR$(13) "WAS SOLL ROCKO TUN" : PRINT CHR$(13);
1220 OPEN 50,0 : INPUT#50,EG$ :REM INPUT EG$
1221 CLOSE 50: GOSUB 1228
1223 FOR I=1 TO WD
1225 NEXT
1226 GOTO 1255
1228 LT$="":WD=1
1229 FOR I=1 TO LEN(EG$)
1230 LT$=MID$(EG$,I,1)
1231 IF LT$=" " THEN WD=WD+1:NEXT
1232 WD$(WD)=WD$(WD)+LT$
1233 NEXT
1234 RETURN

1255 REM EG$="":INPUT EG$
1260 IF EG$="" THEN END
1262 IF LEFT$(EG$,3)="INV" THEN GOTO 5500
1263 IF WD$(1)="UNTERSUCHE" THEN GOTO 6000
1264 IF WD$(1)="TRINKE" THEN GOTO 6500
1265 IF WD$(1)="NIMM" THEN GOTO 7000
1266 IF WD$(1)="GEHE" THEN GOTO 7500
1267 IF WD$(1)="SUCHE" THEN GOTO 8500
1268 IF WD$(1)="REDE" THEN GOTO 40000
1269 AR=VAL(EG$)
1270 IF AR <0 OR AR>RA THEN END
1271 LE=LE-1:IF LE=0 THEN 32000
1275 GOTO 505
4900 REM LE ANPASSEN, CHECK AUF LE = 0
4905 LE=LE-1

5000 REM AUSWERTUNGEN FUER DAS SUCHEN
5037 IF ST%(AR,0)=4 THEN ST%(AR,0)=3
5040 IF ST%(AR,0)>0 THEN PRINT WT$(ST%(AR,0)):GOTO 5300
5050 T=VAL(ST$(AR,0))
5150 FOR I=1 TO T
5200 ? ST$(AR,I);
5250 NEXT
5255 REM ***** DIE SUCHE AENDERT DIE SICHTBARKEIT VON GEGENSTAENDEN"
5265 FOR I=1 TO GS
5270 IF GS%(I,0)=AR AND GS%(I,1)=1 THEN GS%(I,1) = 2
5280 REM GS%(I,1) = GS%(I,1) OR 1
5290 NEXT I
5295 REM ***** SUCHZAHLER ERHOEHEN FUER DIE UNTERSCHIEDLICHEN TEXTE
5300 ST%(AR,0)=ST%(AR,0)+1
5315 GOSUB 50
5320 GOTO 505
5500 REM BEFEHL INVENTAR
5510 REM AUSWERTUNG FUR DEN , SPRUNG VON 1261
5550 REM PRINT"ER HAT";:PRINT DM;:PRINT"DM"
5560 PRINT CHR$(13) "INVENTAR:";
5570 FOR I=1 TO GS:IF GS%(I,0)=-2 THEN PRINT GS$(I,0) " ";
5580 NEXT I
5590 GOSUB 50
5600 GOTO 505
6000 REM *********************************************************************
6001 REM AUSWERTUNGEN FUER DAS UNTERSUCHEN, SPRUNG VON 1264
6002 REM
6003 REM
6004 REM
6030 REM ***** GEGESTAND AUF INV PRUEFEN, SONST SAGEN HAB ICH NICHT
6035 REM ***** GEGENSTANDTEXT AUSGEBEN
6040 IF WD$(2) ="" THEN PRINT"LUFT HALT...": GOTO 6100
6045 FOR I=1 TO GS
6050 IF WD$(2)=GS$(I,0) AND GS%(I,0)=-2 THEN PRINT GS$(I,1):GOTO 6150
6055 NEXT I
6060 PRINT "WAS ER NICHT HAT KANN ER NICHT UNTERSUCHEN!"
6100 GOSUB 50
6120 GOTO 505
6149 REM SPEZIALAUSWERTUNGEN FUER GEGENSTAENDE UNTERSUCHEN
6150 IF I=6 THEN GS$(6,0)="COUPON-KONFERENZ"
6155 IF I=5 THEN GS$(5,0)="PLASTIKTUETE":TK=10
6160 GOTO 6100

6500 REM *********************************************************************
6501 REM AUSWERTUNGEN FUER DAS TRINKEN, SPRUNG VON 1264
6502 REM
6503 REM
6504 REM
6530 REM ***** GEGESTAND AUF INV PRUEFEN, SONST SAGEN HAB ICH NICHT
6535 REM ***** GEGENSTANDTEXT AUSGEBEN
6540 IF WD$(2) ="" THEN PRINT"SCHMECKT IRGENDWIE TROCKEN SO NIX...": GOTO 6600
6545 FOR I=1 TO GS
6550 IF WD$(2)=GS$(I,0) AND GS%(I,0)=-2 AND GS%(I,3) > 0 THEN GOTO 6560
6555 NEXT I
6556 PRINT "DAS HAB ICH GAR NICHT!" : GOTO 6600
6560 PRINT GS$(I,2): GS%(I,0)=-3 : LE = LE + 5
6565 IF LE > 9 THEN LE = 9
6600 GOSUB 50
6620 GOTO 505

7000 REM *** AUSWERTUNG FUR DEN BEFEHL NIMM, SPRUNG VON 1263 ***
7060 IF WD$(2) ="" THEN PRINT"ER GREIFT INS LEERE...": GOTO 7110
7065 GOSUB 34000
7066 IF GG>=TK THEN GOTO 7125
7070 FOR I=1 TO GS
7080 IF (WD$(2)=GS$(I,0) AND GS%(I,1)=2 AND GS%(I,0)=AR) THEN 7130
7090 NEXT I
7100 PRINT "SIEHT ER NICHT! MACH MAL DEINE AUGEN AUF!"
7110 GOSUB 50
7120 GOTO 505
7125 PRINT "ER HAT NUR ZWEI HAENDE, MEHR KANN ER NICHT TRAGEN!"
7126 GOTO 7110

7130 REM *** GO FUER DAS AUFNEHMEN DER GEGENSTAENDE ***
7170 IF GS%(I,4)>0 THEN GOTO 8000
7180 IF GS%(I,2)=2 THEN PRINT "ER NIMMT: ",GS$(I,0): GS%(I,0)=-2: GOTO 7110
7190 GOTO 7110

7500 REM *********************************************************************
7501 REM AUSWERTUNG FUER GEHE 1266
7502 REM
7510 IF WD$(2) ="" THEN PRINT"ER BLEIBT EINFACH STEHEN...": GOTO 7710
7515 IF WD$(2) =RN$(AR,0) THEN PRINT "DA IST ER DOCH SCHON..." : GOTO 7710
7516 IF WD$(2)="ZUG" AND AR=2 THEN GOTO 33050
7517 IF WD$(2)="LIMOKONFERENZ" AND AR=8 AND GS%(12,0)<>-3 THEN GOTO 33080
7529 FOR I=1 TO RA
7530 IF WD$(2)=RN$(I,0) THEN GZ=I:GOTO 7600
7540 NEXT I
7560 PRINT "DEN ORT KENNT ER NICHT...": GOTO 7710
7600 FOR I=1 TO ER(AR,0)
7610 IF ER(AR,I)=GZ THEN AR=GZ:GOTO 1271
7620 NEXT I
7630 PRINT "DA KANN ER NICHT HINGEHEN...": GOTO 7710
7710 GOSUB 50
7720 GOTO 505

8000 REM *** GO FUER KAUFENFUNKTION ***
8005 PRINT"DAS MUSS ER BEZAHLEN. SOLL ER "; GS%(I,4) "DM DAFUER GEBEN"
8010 INPUT EG$
8015 IF EG$="JA" THEN 8040
8020 IF EG$="NEIN" THEN 7110
8030 PRINT "WAS DENN NUN. JA ODER NEIN!" : GOTO 8010
8040 IF DM < GS%(I,4) THEN PRINT "SO VIEL HAT ER GAR NICHT!" : GOTO 7110
8045 PRINT "ROCKO KAUFT:" ,GS$(I,0): GS%(I,0)=-2: DM=DM-GS%(I,4) :GOTO 7110

8500 REM *********************************************************************
8501 REM NEUE SUCHFUNKTION FUER RAEUME - STATT GENERISCHER RAUMSUCHE
8502 REM SOLL HIER NACH SPEZIFISCHEN DINGEN IM RAUM GESUCHT WERDEN.
8503 REM *********************************************************************
8510 IF WD$(2)="" THEN PRINT"ER SUCHT DIE LUFT AB...ER FINDET..LUFT":GOTO 8990
8511 I=0
8515 IF AR = 1  THEN GOSUB 9000
8516 IF AR = 2  THEN GOSUB 9050
8517 IF AR = 3  THEN GOSUB 9100
8518 IF AR = 4  THEN GOSUB 9150
8519 IF AR = 5  THEN GOSUB 9200
8520 IF AR = 6  THEN GOSUB 9250
8521 IF AR = 7  THEN GOSUB 9300
8522 IF AR = 8  THEN GOSUB 9350
8523 IF AR = 9  THEN GOSUB 9400
8524 IF AR = 10 THEN GOSUB 9500
8525 IF AR = 11 THEN GOSUB 9600
8530 IF AR = 12 THEN GOSUB 9700
8895 IF I=0 THEN PRINT "ER FINDET NICHTS..." : GOTO 8990
8900 PRINT ST%(I,0):PRINT "": PRINT I: IF ST%(I,0)=5 THEN ST%(I,0)=4
8901 IF ST%(I,0)>0 THEN PRINT WT$(ST%(I,0)): GOTO 8915
8902 GOSUB 33950
8903 FOR T=1 TO VAL(ST$(I,0))
8905 PRINT ST$(I,T)
8910 NEXT T
8915 ST%(I,0)= ST%(I,0) +1

8990 GOSUB 50
8995 GOTO 505

8999 REM DAS HIER FOLGENDE I IST FUER DIE AUSGABE DES PASSENDEN SUCHTEXTES
9000 IF WD$(2)="STRASSE"       AND GS%(3,1)=1 THEN I=1: GS%(3,1)=2:GOTO 8900
9005 IF WD$(2)="GEHWEG"        AND GS%(3,1)=1 THEN I=1: GS%(3,1)=2:GOTO 8900
9010 IF WD$(2)="MUELL"         AND GS%(3,1)=1 THEN I=1: GS%(3,1)=2:GOTO 8900
9020 RETURN

9050 IF WD$(2)="BAHNSTEIG"     AND GS%(5,1)=1 THEN I=2: GS%(5,1)=2:GOTO 8900
9051 RETURN

9100 IF WD$(2)="REGAL"         AND GS%(4,1)=1 THEN I=3: GS%(4,1)=2:GOTO 8900
9105 IF WD$(2)="ZEITSCHRIFTEN" AND GS%(4,1)=1 THEN I=3: GS%(4,1)=2:GOTO 8900
9110 IF WD$(2)="KUEHLSCHRANK"  AND GS%(2,1)=1 THEN I=8: GS%(2,1)=2:GOTO 8900
9115 IF WD$(2)="LIMO"          AND GS%(2,1)=1 THEN I=8: GS%(2,1)=2:GOTO 8900
9116 IF WD$(2)="AUTOMAT"                      THEN I=15           :GOTO 8900
9120 RETURN

9250 IF WD$(2)="TAFEL"         AND GS%(6,1)=1 THEN I=6: GS%(6,1)=2:GOTO 8900
9255 IF LEFT$(WD$(2),3)="PLAK" AND GS%(6,1)=1 THEN I=6: GS%(6,1)=2:GOTO 8900
9260 RETURN

9300 IF LEFT$(WD$(2),4)="TISC" AND GS%(1,1)=1 THEN I=7: GS%(1,1)=2:GOTO 8900
9301 RETURN

9350 IF LEFT$(WD$(2),3)="MES"                 THEN I=16          :GOTO 8900
9351 RETURN

9400 IF WD$(2)="REGALE" AND GS%(7,1)=1 THEN I=9:GS%(7,1)=2:GS%(13,1)=2:GOTO 8900
9401 IF WD$(2)="ZEUG"   AND GS%(7,1)=1 THEN I=9:GS%(7,1)=2:GS%(13,1)=2:GOTO 8900
9402 IF WD$(2)="C64"           AND GS%(8,1)=1 THEN I=10:GS%(8,1)=2:GOTO 8900
9403 IF LEFT$(WD$(2),3)="GIA"  AND GS%(8,1)=1 THEN I=10:GS%(8,1)=2:GOTO 8900
9404 IF WD$(2)="SCHREIBTISCH"  AND GS%(8,1)=1 THEN I=10:GS%(8,1)=2:GOTO 8900
9410 RETURN

9500 IF WD$(2)="MITARBEITER"   AND GS%(9,1)=1 THEN I=11:GS%(9,1)=2:GOTO 8900
9505 IF WD$(2)="BUEHNE"        AND ER(10,0)=1 THEN I=12:ER(10,0)=2:GOTO 8900
9510 RETURN

9600 IF WD$(2)="KISTEN"        AND GS%(10,1)=1 THEN I=13:GS%(10,1)=2:GOTO 8900
9605 IF LEFT$(WD$(2),4)="STUD" AND GS%(10,1)=1 THEN I=13:GS%(10,1)=2:GOTO 8900
9610 IF LEFT$(WD$(2),4)="TISC" AND GS%(11,1)=1 THEN I=14:GS%(11,1)=2:GOTO 8900
9615 RETURN

9700 RETURN

10000 REM RAUMTEXTE
10100 RN$(1,0)="BUERO"
10101 RN$(1,1)="8"
10105 RT$(1,0)="ROCKO STEHT VOR EINEM HOHEN GEBAEUDE.   "
10110 RT$(1,1)="JEDEN TAG KOMMT ER HIER HER. WAS ER DORT"
10115 RT$(1,2)="ZU TUN HAT? ER REDET NICHT DARUEBER, IST"
10120 RT$(1,3)="ER ETWA EIN AGENT UND VERSUCHT DIE LIMO-"
10125 RT$(1,4)="GEHEIMFORMEL ZUR WELTHERRSCHAFT VOR DER "
10130 RT$(1,5)="MAECHTIGEN WASSERLOBBY ZU VERSTECKEN?   "
10135 RT$(1,6)="DIE TUEREN SIND MASSIV UND AKTUELL ZU.  "
10140 RT$(1,7)="NEBEN DEM GEHWEG AN DER STRASSE LIEGT SO"
10145 RT$(1,8)="EINIGES AN MUELL."
10150 ER(1,0)=3:ER(1,1)=7:ER(1,2)=3:ER(1,3)=2
10200 RN$(2,0)="BAHNHOF"
10201 RN$(2,1)="8"
10205 RT$(2,0)="ER STEHT AN EINEM NORMALEN BAHNHOF. HIER"
10210 RT$(2,1)="UND DA ETWAS DRECKIG, SONST LANGWEILIG. "
10215 RT$(2,2)="DER ZUG KOMMT ALLE 5 MINUTEN - NAJA WENN"
10220 RT$(2,3)="ER DENN KOMMT WAS MAN BEI DER BAHN JA..."
10225 RT$(2,4)="LASSEN WIR DAS THEMA. ABER DA ROLLT AUCH"
10230 RT$(2,5)="SCHON EIN ZUG EIN. AM BAHNSTEIG STEHEN  "
10235 RT$(2,6)="NUR WENIGE MITFAHRER.ALLE SIND VERTIEFT "
10240 RT$(2,7)="UND HABEN EINEN KLEINEN GRAUEN KASTEN IN"
10245 RT$(2,8)="DEN HAENDEN AUS DENEN LEISE MUSIK DUDELT"
10250 ER(2,0)=2:ER(2,1)=1:ER(2,2)=4
10300 RN$(3,0)="KIOSK"
10301 RN$(3,1)="8"
10305 RT$(3,0)="DER GUTE ALTE KIOSK. SCHON ALS KLEINES  "
10310 RT$(3,1)="KIND KAM ER HIER HER UM SICH EINE BUNTE "
10315 RT$(3,2)="TUETE ZU KAUFEN.HEUTE GIBT ES HIER IMMER"
10320 RT$(3,3)="NOCH ZUCKERZEUG ABER AUCH KUEHLE LIMO IM"
10325 RT$(3,4)="KUEHLSCHRANK. LINKS STEHT EIN REGAL MIT "
10330 RT$(3,5)="ZEITSCHRIFTEN. GANZ HINTEN RECHTS STEHT "
10335 RT$(3,6)="EIN AUTOMAT. DER KIOSBETREIBER LAECHELT "
10340 RT$(3,7)="IHN FREUNDLICH AN. MIT EINEM 'HI ROCKO' "
10345 RT$(3,8)="BEGRUESST ER SEINEN STAMMKUNDEN.        "
10350 ER(3,0)=1:ER(3,1)=1
10400 RN$(4,0)="ZUG"
10401 RN$(4,1)="6"
10405 RT$(4,0)="DER ZUG STEHT.DIE HALTESTELLE NENNT SICH"
10410 RT$(4,1)="AACHNER PRINTENMUSEUM, EINE DER BESTEN "
10415 RT$(4,2)="SEHENSWUERDIGKEITEN HIER IN DER NAEHE,ER"
10420 RT$(4,3)="WAR ABER NOCH NIE DA,GIBT DA KEINE LIMO."
10425 RT$(4,4)="DER BAHNHOFSSCHALTER IST VERWAIST. DURCH"
10430 RT$(4,5)="DAS DRECKIGE FENSTER SIEHT ER NUR EIN   "
10435 RT$(4,6)="PAAR LEERE LIMOS AUF DEM BODEN STEHEN.  "
10450 ER(4,0)=3:ER(4,1)=5:ER(4,2)=6:ER(4,3)=2
10500 RN$(5,0)="WARTEHAUS"
10501 RN$(5,1)="4"
10505 RT$(5,0)="EIN KLEINES WARTEHAUS,WENN ES MAL WIEDER"
10506 RT$(5,1)="LANEGER DAUERT MIT DER BAHN, ALSO FAST  "
10510 RT$(5,2)="IMMER.ROCKO SETZT SICH KURZ HIN. WEITER "
10515 RT$(5,3)="GEHTS HIER ABER NICHT - UEBERALL UM DAS "
10520 RT$(5,4)="HAUS HERUM IST EINE RIESEN BAUSTELLE.   "
10550 ER(5,0)=1:ER(5,1)=4
10600 RN$(6,0)="TAXISTAND"
10601 RN$(6,1)="8"
10605 RT$(6,0)="DER UEBLICHE TAXISTAND NEBEN EINEM BAHN-"
10606 RT$(6,1)="HOF. LEIDER IST GERADE GAR KEIN TAXI DA."
10610 RT$(6,2)="NEBEN DER STRASSE SIEHT ER EINE TAFEL   "
10615 RT$(6,3)="MIT DIVERSEN PLAKATEN.                  "
10620 RT$(6,4)="AUSSER IHM IST HIER KEINE MENSCHENSEELE,"
10625 RT$(6,5)="SO EINLADEND WIE DAS HIER AUSSIEHT KEIN "
10630 RT$(6,6)="WUNDER DENKT ER SICH NUR. ETWAS WEITER  "
10635 RT$(6,7)="IM NORDEN DER STRASSE NACH SIEHT ER DAS "
10640 RT$(6,8)="PRINTEN-CENTER, EINE SHOPPING-MALL.     "
10650 ER(6,0)=2:ER(6,1)=4:ER(6,2)=8
10700 RN$(7,0)="IMBISS"
10701 RN$(7,1)="11"
10705 RT$(7,0)="ROCKO STEHT VOR SEINEM LIEBLINGS-IMBISS."
10710 RT$(7,1)="CHEZ-MARIO STEHT UBER DER TUER. AN EINEM"
10720 RT$(7,2)="DER TISCHE SITZT EIN PAERCHEN UND TEILT "
10725 RT$(7,3)="SICH EINEN TELLER NUDELN. WER DEN FILM  "
10730 RT$(7,4)="SUSI UND STROLCHI KENNT WEISS WAS ER NUN"
10735 RT$(7,5)="GERADE SIEHT.                           "
10740 RT$(7,6)="DIE BEIDEN MARIO-BRUEDER BEREITEN HIER  "
10745 RT$(7,7)="DIE BESTE CURRYWURST IM GANZEN VIERTEL  "
10750 RT$(7,8)="ZU. BEIM GEDANKEN AN DEN BESUCH IN DER  "
10755 RT$(7,9)="HEUTIGEN MITTAGSPAUSE HAT ER SOFORT DEN "
10760 RT$(7,10)="GESCHMACK VON LIMO-CURRY-SAUCE AUF DER  "
10765 RT$(7,11)="ZUNGE.                                  "
10770 ER(7,0)=1:ER(7,1)=1
10800 RN$(8,0)="EINKAUFSPASSAGE"
10801 RN$(8,1)="7"
10805 RT$(8,0)="DAS PRINTENCENTER IST BEKANNT FUER SEINE"
10810 RT$(8,1)="LECKEREN SPEZIALITAETEN.DANEBEN GIBT ES "
10815 RT$(8,2)="ABER AUCH EIN COMPUTERGESCHAEFT, EINE   "
10820 RT$(8,3)="WERKSTATT UND EINEN MESSEBEREICH.       "
10825 RT$(8,4)="HIER IM ZENTRALEN BEREICH IST ER ABER   "
10830 RT$(8,5)="ERST MAL UEBERWAELTIGT VON DEN VIELEN   "
10835 RT$(8,6)="MENSCHEN UND GERUECHEN. UND DAMIT MEINT "
10840 RT$(8,7)="ER NICHT DEN GERUCH FRISCHER PRINTEN... "
10845 ER(8,0)=4:ER(8,1)=6:ER(8,2)=9:ER(8,3)=10:ER(8,4)=12
10900 RN$(9,0)="COMPUTERLADEN"
10901 RN$(9,1)="10"
10905 RT$(9,0)="DEN LADEN KENNT ROCKO SCHON SEHR LANGE. "
10910 RT$(9,1)="ALS KLEINER STEPPKE HAT ER HIER AUF DEN "
10915 RT$(9,2)="AUSGESTELLTEN COMPUTERN GESPIELT.UND DER"
10920 RT$(9,3)="VERKAEUFER IST IMMER NOCH DER GLEICHE!  "
10925 RT$(9,4)="ALS ER ROCKO ERKENNT SAGT ER -SCHOEN DAS"
10930 RT$(9,5)="DU MAL WIEDER DA BIST.ICH HAB HIER DIE  "
10935 RT$(9,6)="NEUE GIANA SISTERS VERSION FUER DICH!-  "
10940 RT$(9,7)="ANSONSTEN IST DER LADEN VOLL MIT REGALEN"
10945 RT$(9,8)="IN DENEN ALLES MOEGLICHE AN ZEUG STEHT. "
10950 RT$(9,9)="NEBEN DEM TRESEN STEHT DER GUTE ALTE C64"
10955 RT$(9,10)="AUF EINEM SCHREIBTISCH.                 "
10960 ER(9,0)=1:ER(9,1)=8
11000 RN$(10,0)="LIMOKONFERENZ"
11005 RN$(10,1)="10"
11011 RT$(10,1)="AUF DER BUEHNE SITZEN EIN PAAR ALTE GE- "
11012 RT$(10,2)="SELLEN UND OEFFNEN EINE LIMO NACH DER   "
11013 RT$(10,3)="ANDEREN. SO GERNE WAERE ROCKO AUCH DABEI"
11014 RT$(10,4)="ABER BISHER HABEN SIE IHN NICHT DAZU    "
11015 RT$(10,5)="EINGELADEN,ER IST HALT UNBEKANNT.WENN ER"
11016 RT$(10,6)="MIT DEM STREAMEN ERFOLGREICHER WIRD     "
11017 RT$(10,7)="VIELLEICHT! DER AKTUELLE SPONSOR LOSCHER"
11018 RT$(10,8)="IST UEBERALL ZU SEHEN. VOR DER BUEHNE   "
11019 RT$(10,9)="STEHEN EINIGE MITARBEITER UND VERTEILEN "
11020 RT$(10,10)="GESCHMACKSPROBEN.                       "
11025 ER(10,0)=1:ER(10,1)=8:ER(10,2)=11

11100 RN$(11,0)="BACKSTAGE"
11105 RN$(11,1)="13"
11111 RT$(11,1)="ER GREIFT AN DEN TUERKNAUF UND DREHT -  "
11112 RT$(11,2)="OFFEN! ER GEHT IN DEN RAUM UND SIEHT 2  "
11113 RT$(11,3)="GESTALTEN AN EINEM TISCH SITZEN. ER KANN"
11114 RT$(11,4)="SIE REDEN HOEREN.-MENSCH CIRDAN, DER PET"
11115 RT$(11,5)="IST DEUTLICH BESSER ALS DER C64! - DER  "
11116 RT$(11,6)="AELTERE MANN MIT SCHUETTEREM HAAR, AUF  "
11117 RT$(11,7)="SEINER BRUST EIN AUFKLEBER MIT -PINBALL "
11118 RT$(11,8)="LOVER-, ANTWORTET: HARLEY, NIEMALS! DER "
11119 RT$(11,9)="C64 HATTE DEN SID, DA KANN DER PET NICHT"
11120 RT$(11,10)="GEGEN ANSTINKEN, TROTZ PETUNIA SOUND.   "
11125 RT$(11,11)="ROCKO VERSTEHT NUR BAHNHOF. ANSONSTEN   "
11130 RT$(11,12)="SIEHT ER EINEN TISCH MIT TISCHDECKE UND "
11135 RT$(11,13)="EIN PAAR KISTEN MIT STUDIOMATERIAL.     "
11140 ER(11,0)=1:ER(11,1)=8

11200 RN$(12,0)="WERKSTATT"
11205 RN$(12,1)="12"
11211 RT$(12,1)="IM SCHAUFENSTER STEHT VIEL ELEKTRONISCH "
11212 RT$(12,2)="AUSSEHENDES ZEUG. PLATINEN, LOETKOLBEN  "
11213 RT$(12,3)="UND JEDE MENGE, WAS ER NICHTMAL ERKENNT."
11214 RT$(12,4)="HINTER EINEM TRESEN SITZEN ZWEI JUNGE   "
11215 RT$(12,5)="LEUTE. DER MANN HAT VOR SICH EINE LUPE  "
11216 RT$(12,6)="UND FOKUSSIERT SICH AUF EINE QUALMENDE  "
11217 RT$(12,7)="PLATINE. 'MIST, SCHON WIEDER IST DIE PSU"
11218 RT$(12,8)="DIODE DURCHGEKOKELT!' SAGT ER. NEBEN IHM"
11219 RT$(12,9)="SITZT EINE FRAU UND STREICHELT UBER DAS "
11220 RT$(12,10)="FELL EINER KATZE DIE SICH AUF DEM TISCH "
11225 RT$(12,11)="RAEKELT. MIT DER ANDEREN HAND BEMALT SIE"
11230 RT$(12,12)="CHIPS MIT SCHWARZEM NAGELLACK.          "
11240 ER(12,0)=1:ER(11,1)=8

19990 REM **********************************************
19991 REM * AB HIER DIE TEXTE FUER DIE BEFEHLE
19992 REM * ARRAY ST = SUCHTEXTE ; ST$(RAUMNUMMER,0)=X
19993 REM * DAS X STEHT FUER DIE ANZAHL DER TEXTZEILEN
19994 REM * ARRAY ST%(X,0)=0 NOCH NICHT GESUCHT, =1 BEREITS GESUCHT
19995 REM **********************************************
20000 REM
20100 ST$(1,0)="4"
20105 ST$(1,1)="NEBEN DEM BORDSTEIN FINDET ER EINEN    "
20110 ST$(1,2)="KRONKORKEN! DER SOLLTE IN DIE SAMMLUNG."
20115 ST$(1,3)="SCHEINT EINE GUTE LIMO GEWESEN ZU SEIN."
20120 ST$(1,4)="ER IST GELB UND MAN ERKENNT EINE TREPPE"
20200 ST$(2,0)="5"
20205 ST$(2,1)="ER SCHAUT SICH AM BAHNSTEIG SORGFAELTIG"
20210 ST$(2,2)="UM. IM RAUCHERBEREICH ENTDECKT ER AUF  "
20215 ST$(2,3)="DEM BODEN EIN GANZ WUNDERBARES EXEMPLAR"
20220 ST$(2,4)="EINES HOCHMODERNEN CHEMISCHEN PRODUKTS:"
20225 ST$(2,5)="GENAUER GESAGT - EINEN PLASTIKKLUMPEN. "
20305 ST$(3,0)="4"
20310 ST$(3,1)="DIVERSE ZEITUNGEN. KLINGENDE NAMEN WIE "
20315 ST$(3,2)="DIE PRALINE BIS HANDWERKERZEITUNGEN DIE"
20320 ST$(3,3)="SCHLUESSELLOCH HEISSEN - ALLES DABEI.  "
20325 ST$(3,4)="ABER DAS YPS-HEFT ERREGT SEIN INTERESSE"
20405 ST$(4,0)="3"
20410 ST$(4,1)="UNTER EINEM DER SITZE FINDET ER EINE PFANDFLASCHE."
20415 ST$(4,2)="UND DA - IM GEPAECKNETZ EINE WEITERE!. DIE NIMMT ER"
20420 ST$(4,3)="NATUERLICH SOFORT MIT."
20505 ST$(5,0)="2"
20510 ST$(5,1)="NEBEN DER SITZBANK STEHT EINE LEERE DOSE ENERGY."
20515 ST$(5,2)="PFAND GIBTS DAFUER AUCH, ALSO STECKT ER SIE EIN."
20605 ST$(6,0)="3"
20610 ST$(6,1)="NEBEN DIVERSEN -WIR KAUFEN IHR AUTO-   "
20615 ST$(6,2)="KARTEN ENTDECKT ER AUF DEM BODEN EINEN "
20620 ST$(6,3)="COUPON."
20700 ST$(7,0)="5"
20705 ST$(7,1)="ROCKO SCHAUT SICH EIN WENIG UM UND ENT-"
20710 ST$(7,2)="DECKT DANN UNTER DEM STUHL AUF DEM ER  "
20715 ST$(7,3)="GESESSEN HAT EINE FAHRKARTE! EIN WENIG "
20720 ST$(7,4)="ZERKNAUTSCHT UND MIT SOSSENFLECKEN ABER"
20725 ST$(7,5)="WIRD SCHON GEHEN.                      "
20800 ST$(8,0)="3"
20805 ST$(8,1)="DER KUEHLSCHRANK IST FAST LEER. NEBEN  "
20810 ST$(8,2)="DIVERSEN ENERGY DRINKS ENTDECKT ER NUR "
20815 ST$(8,3)="NOCH EINE EINZIGE LIMO!
20900 ST$(9,0)="7"
20905 ST$(9,1)="IM REGAL STEHT JEDE MENGE TOLLES ZEUG. "
20910 ST$(9,2)="JOYSTICKS, KABEL, SPIELE OHNE ENDE AUF "
20915 ST$(9,3)="DISKETTE UND DATASETTE. ABER DAVON HAT "
20920 ST$(9,4)="ER EIGENTLICH SCHON ALLE. RECHTS SIEHT "
20925 ST$(9,5)="ROCKO ETWAS NEUES - EINE KAMERA DIE MAN"
20930 ST$(9,6)="DIREKT AN DEN C64 ANKLEMMEN KANN! AUF  "
20993 ST$(9,7)="DEM ZETTEL STEHT - IDEAL ZUM STREAMEN! "
21000 ST$(10,0)="4"
21005 ST$(10,1)="IM C64 FLOPPY LAUFWERK STECKT EINE     "
21010 ST$(10,2)="DISKETTE DRIN. ER OEFFNET DAS LAUFWERK "
21015 ST$(10,3)="UND HOLT SIE RAUS - GIANA SISTERS 3OTH "
21020 ST$(10,4)="ANNIVERSARY STEHT DRAUF.               "
21100 ST$(11,0)="6"
21105 ST$(11,1)="ER STELLT SICH AN EINEN DER TISCHE UND "
21110 ST$(11,2)="SOFORT ZEIGT IHM EINER DER DORTIGEN    "
21115 ST$(11,3)="MITARBEITER EINE KUEHLE FLASCHE LOSCHER"
21120 ST$(11,4)="BITTE BEDIENEN SIE SICH SAGT ER ZU IHM "
21125 ST$(11,5)="DAS LAESST SICH ROCKO SICHER NICHT ZWEI"
21130 ST$(11,6)="MAL SAGEN!                             "
21150 ST$(12,0)="5"
21155 ST$(12,1)="DIE BUEHNE HAT AUF DER RUECKSEITE EINEN"
21160 ST$(12,2)="STOFFVORHANG. ALS ER DIESEN BEISEITE   "
21165 ST$(12,3)="SCHIEBT ERKENNT ER EINE TUER, SIE IST  "
21170 ST$(12,4)="MIT -BACKSTAGE, ZUTRITT NUR FUER VIPS!-"
21175 ST$(12,5)="BESCHRIFTET."
21200 ST$(13,0)="4"
21205 ST$(13,1)="DIE KISTEN ENTHALTEN JEDE MENGE ZEUG.  "
21210 ST$(13,2)="UNTER EINEM BERG KABEL ENTDECKT ER ABER"
21215 ST$(13,3)="ETWAS TOLLES. EIN MIKROFON FUER DEN C64"
21220 ST$(13,4)="DAS KANN ER DOCH SUPER GEBRAUCHEN.     "
21250 ST$(14,0)="3"
21255 ST$(14,1)="ER HEBT DIE TISCHDECKE AN UND SIEHT EI-"
21260 ST$(14,2)="NEN KANISTER.-KETTENSAEGENBENZIN,ZAK-  "
21265 ST$(14,3)="STEHT DRAUF.KANN MAN IMMER BRAUCHEN... "
21270 ST$(15,0)="3"
21275 ST$(15,1)="EIN AUTOMAT DER BAHN. AUF DEM DISPLAY  "
21280 ST$(15,2)="IST EIN BLUESCREEN ZU SEHEN: KERNEL    "
21285 ST$(15,3)="PANIK - OUT OF 2BYTE MEMORY, BASIC V19 "
21290 ST$(16,0)="4"
21295 ST$(16,1)="DER ZUGANG ZUM MESSEBEREICH SCHEINT SO "
21300 ST$(16,2)="NICHT DIREKT MOEGLICH ZU SEIN. ER SIEHT"
21305 ST$(16,3)="MENSCHEN VOR DEM EINGANG IN EINER LAN- "
21310 ST$(16,4)="GEN SCHLANGE STEHEN. DA MUSS ER DURCH. "




29900 REM ***** SUCHTEXTE FUER WEITERE SUCHEN
29901 WT$(1)="ER SUCHT WEITER... FINDET ABER NICHTS."
29902 WT$(2)="TROTZDEM SUCHT ER... UND FINDET IMMER NOCH NICHTS."
29903 WT$(3)="BEIM VIERTEN MAL FINDET ER ETWAS - ODER NEIN DOCH NICHT"
29904 WT$(4)="EY, WIE OFT WILLST DU NOCH HIER SUCHEN!!"

29990 REM ****************************************************************
29991 REM * AB HIER DIE TEXTE FUER DIE GEGENSTAENDE
29992 REM * ARRAY GS ; GS$(NUMMER,0)="NAME"
29993 REM * ARRAY GS ; GS$(NUMMER,1)="TEXT"
29994 REM * ARRAY GS ; GS$(NUMMER,2)="BRAUCHEN-TEXT"
29995 REM * ARRAY GS ; GS%(NUMMER,0)=RAUMNUMMER
29996 REM * ARRAY GS ; GS%(NUMMER,1)=SICHTBAR(1=NEIN,2=JA)
29997 REM * ARRAY GS ; GS%(NUMMER,2)=NEHMBAR (1=NEIN,2=JA)
29998 REM * ARRAY GS ; GS%(NUMMER,3)=VERBRAUCHBAR(0=NICHT)!
29999 REM * ARRAY GS ; GS%(NUMMER,4)=PREIS IN DM
30000 REM ****************************************************************
30010 GS$(1,0)="FAHRKARTE"
30011 GS$(1,1)="EINE FAHRKARTE, MIT LIMO-CURRY SAUCE BESCHMIERT"
30012 GS$(1,2)="ER ZEIGT SIE DEM KONTROLLEUR - UND DER LOCHT SIE."
30013 GS%(1,0)=7
30014 GS%(1,1)=1
30015 GS%(1,2)=2
30016 GS%(1,3)=0
30017 GS%(1,4)=0
30020 GS$(2,0)="FLENSLIMO"
30021 GS$(2,1)="EINE FLENS-LIMO, DIE MIT DEM PLOPP!"
30022 GS$(2,2)="LECKER! DIE LEERE FLASCHE KOMMT AUF DEN MUELLEIMER."
30023 GS%(2,0)=3
30024 GS%(2,1)=1
30025 GS%(2,2)=2
30026 GS%(2,3)=2
30027 GS%(2,4)=2
30030 GS$(3,0)="KRONKORKEN-GELB"
30031 GS$(3,1)="TOLLER GELBER KRONKOREN FUER DIE SAMMLUNG"
30032 GS$(3,2)="DAS KANN ER SO NICHT BENUTZEN"
30033 GS%(3,0)=1
30034 GS%(3,1)=1
30035 GS%(3,2)=2
30036 GS%(3,3)=0
30037 GS%(3,4)=0
30040 GS$(4,0)="YPSHEFT"
30041 GS$(4,1)="MIT GIMMICK!"
30042 GS$(4,2)="DAS HEFT KANN ER SO NICHT BENUTZEN"
30043 GS%(4,0)=3
30044 GS%(4,1)=1
30045 GS%(4,2)=2
30046 GS%(4,3)=0
30047 GS%(4,4)=3
30050 GS$(5,0)="PLASTIKKLUMPEN"
30051 GS$(5,1)="DER KLUMPEN IST - EINE PLASTIKTUETE!"
30052 GS$(5,2)="NUN KANN ER DEUTLICH MEHR DINGE TRAGEN."
30053 GS%(5,0)=2
30054 GS%(5,1)=1
30055 GS%(5,2)=2
30056 GS%(5,3)=0
30057 GS%(5,4)=0
30060 GS$(6,0)="COUPON"
30061 GS$(6,1)="COUPON: 1X FREIER EINTRITT LIMOKONFERENZ!"
30062 GS$(6,2)="SO KOMMT ER UMSONST REIN!"
30063 GS%(6,0)=6
30064 GS%(6,1)=1
30065 GS%(6,2)=2
30066 GS%(6,3)=0
30067 GS%(6,4)=0
30070 GS$(7,0)="KAMERA"
30071 GS$(7,1)="MIT EINGEBAUTEM NETZWERK ZUM STREAMEN!  "
30072 GS$(7,2)="ER KLEMMT SIE AN SEINEN C64 - PERFEKT!  "
30073 GS%(7,0)=9
30074 GS%(7,1)=1
30075 GS%(7,2)=2
30076 GS%(7,3)=0
30077 GS%(7,4)=10
30080 GS$(8,0)="DISKETTE"
30081 GS$(8,1)="30TH ANNIVERSARY EDITION GIANA-SISTERS."
30082 GS$(8,2)="ER STECKT SIE IN DEN C64 - PASST.      "
30083 GS%(8,0)=9
30084 GS%(8,1)=1
30085 GS%(8,2)=2
30086 GS%(8,3)=0
30087 GS%(8,4)=0
30090 GS$(9,0)="LOSCHER-LIMO"
30091 GS$(9,1)="TOTAL LECKERES GEBRAEU."
30092 GS$(9,2)="TRINKEN UND GUT.       "
30093 GS%(9,0)=10
30094 GS%(9,1)=1
30095 GS%(9,2)=2
30096 GS%(9,3)=2
30097 GS%(9,4)=0
30100 GS$(10,0)="MIKROFON"
30101 GS$(10,1)="MIT DSUB9 ANSCHLUSS FUER DEN C64"
30102 GS$(10,2)="XXX                             "
30103 GS%(10,0)=11
30104 GS%(10,1)=1
30105 GS%(10,2)=2
30106 GS%(10,3)=0
30107 GS%(10,4)=0
30110 GS$(11,0)="KETTENSAEGENBENZIN"
30111 GS$(11,1)="AUF DEM KANISTER STEHT -ZAK- DRAUF."
30112 GS$(11,2)="DAS KANN ER SO NICHT VERWENDEN.    "
30113 GS%(11,0)=11
30114 GS%(11,1)=1
30115 GS%(11,2)=2
30116 GS%(11,3)=0
30117 GS%(11,4)=0
30120 GS$(12,0)="EINTRITTSKARTE"
30121 GS$(12,1)="TAGESKARTE LIMOKONFERENZ           "
30122 GS$(12,2)="SO KOMMT ER AUF DIE LIMOKONFERENZ  "
30123 GS%(12,0)=8
30124 GS%(12,1)=1
30125 GS%(12,2)=2
30126 GS%(12,3)=0
30127 GS%(12,4)=5
30130 GS$(13,0)="JOYSTICK"
30131 GS$(13,1)="DEN KANN ER EH NICHT NEHMEN"
30132 GS$(13,2)="MUSS IRGENDWAS MACHEN KOENNEN      "
30133 GS%(13,0)=9
30134 GS%(13,1)=1
30135 GS%(13,2)=2
30136 GS%(13,3)=0
30137 GS%(13,4)=20
30140 GS$(14,0)="QUICKSHOT"
30141 GS$(14,1)="LEIDER KAPUTT, IRGENDWAS AN DER PLATINE"
30142 GS$(14,2)="MUSS IRGENDWAS MACHEN KOENNEN      "
30143 GS%(14,0)=9
30144 GS%(14,1)=1
30145 GS%(14,2)=2
30146 GS%(14,3)=0
30147 GS%(14,4)=0

31999 RETURN
32000 REM ****************************************************************
32001 REM * GAME OVER ZEUG
32002 REM *
32003 REM *
32004 PRINT"";
32005 PRINT "ROCKO IST VERDURSTET. MACH ES BEIM NACHSTEN MAL EINFACH BESSER!"
32010 GOSUB 50
32015 END
33000 REM ****************************************************************
33001 REM * SPEZIALAUSWERTUNGEN
33002 REM *
33003 REM *
33050 REM *** FAHRKARTE FUER DEN ZUG NOTWENDIG
33051 IF GS%(1,0)= -2 THEN GOTO 33060
33052 PRINT"ER STEIGT IN DEN ZUG EIN, DER KONTROLLEUR KOMMT SOFORT UND WILL"
33053 PRINT" DIE FAHRKARTE SEHEN. ROCKO HAT KEINE UND SO WIRFT DER KONTROLLEUR"
33054 PRINT" IHN EINFACH AUS DEM ZUG."
33055 GOTO 33068
33060 PRINT"ER STEIGT IN DEN ZUG EIN, DER KONTROLLEUR SCHAUT DIE FAHRKARTE"
33061 PRINT" AN. MIT EINEM SCHMOLLEN TACKERT ER SIE AB. DAS BISTRO HAT"
33062 PRINT" GESCHLOSSEN SAGT ER NOCH UND GEHT DANN WEITER ZUM NAECHSTEN"
33063 PRINT" FAHRGAST WO SICH DAS SPIEL WIEDERHOLT."
33064 PRINT" DANN DIE DURCHSAGE:DER ZUG HAT EINEN PLATTEN, ALLES AUSSTEIGEN!"
33065 PRINT" EIN RAUNEN GEHT DURCH DEN ZUG ABER NOTGEDRUNGEN STEIGT ER AUS."
33066 PRINT" DIE FAHRKARTE BRAUCHT ROCKO JA NUN NICHT MEHR, DAHER WIRFT ER SIE"
33067 PRINT" IN EINEN MUELLEIMER." : GS%(1,0)=-3 : AR=4 : ER(4,0)=2
33068 REM DIE OBIGEN ZEILEN WERFEN FAHRKARTE WEG -3, STELLT RAUM AUF ZUG UND
33069 REM LOESCHT DEN LETZTEN ERREICHBAREN RAUM AUS DER LISTE (BAHNHOF)
33070 GOSUB 50
33071 GOTO 505

33079 REM *** EINTRITTSKARTE FUER DIE KONFERENZ NOTWENDIG
33080 GOSUB 33950:IF GS%(12,0)= -2 THEN GOTO 33096
33081 IF GS%(12,0)=8 AND GS%(12,1)=2 THEN GOTO 33122
33082 PRINT "DA IST EIN DURCHGANG DER IN DEN MESSE- "
33083 PRINT "BEREICH ZU FUEHREN SCHEINT.LEIDER KOMMT"
33084 PRINT "MAN NICHT EINFACH SO REIN. DENN EINE   "
33085 PRINT "JUNGE FRAU HAELT DORT TICKETS BEREIT.  "
33086 PRINT "'NUR 5 DM EINTRITT' RUFT SIE ROCKO MIT "
33087 PRINT "CHARMANTEN LAECHELN ZU UND LEGT EINE   "
33088 PRINT "EINTRITTSKARTE AUF EINEN TISCH VOR IHM."
33089 GS%(12,1)=2:
33090 IF GS$(6,0)="COUPON-KONFERENZ" THEN GOSUB 33112
33095 GOTO 33110
33096 PRINT "ER GIBT DIE EINTRITTSKARTE DEM ORDNER, "
33097 PRINT "DIESER KLEBT IHM EIN BAND UM DAS HAND- "
33098 PRINT "GELENK.'DAMIT KOMMEN SIE DEN GANZEN TAG"
33099 PRINT "WIEDER REIN, ALSO GUT AUFPASSEN.' SAGT "
33100 PRINT "ER ROCKO NOCH.DANN OEFFNET ER DIE TUER."
33101 GS%(12,0)=-3 : AR=10
33102 REM DIE OBIGE ZEILE WIRFT EINTRITTSKARTE WEG -3, STELLT RAUM AUF KONF
33110 GOSUB 50
33111 GOTO 505
33112 PRINT "ROCKO ERINNERT SICH AN DEN COUPON! ER  "
33113 PRINT "ZEIGT DIESEN DER JUNGEN FRAU.'ACH SO!NA"
33114 PRINT "DAMIT KOMMEN SIE JA UMSONST REIN.'SAGT "
33115 PRINT "SIE UND NIMMT DIESEN SIE KRAMT IN IHRER"
33116 PRINT "TASCHE UND HOLT EINE KARTE HERAUS, AUF "
33117 PRINT "DIESER IST GRATIS-ZUGANG AUFGEDRUCKT.  "
33118 PRINT "SIE TAUSCHT DIE BEIDEN KARTEN AUS,JETZT"
33119 PRINT "LIEGT DIE GRATIS-KARTE AUF DEM TISCH.  "
33120 GS%(12,4)=0
33121 RETURN
33122 PRINT "DER ORDNER LAESST IHN OHNE KARTE NICHT "
33123 PRINT "REIN.'HOLEN SIE EINE EINTRITTSKARTE'   "
33124 PRINT "SAGT DER ORDNER. VIELLEICHT SOLLTE ER  "
33125 PRINT "DIE EINFACH MAL MITNEHMEN...           "
33126 GOTO 33090
33127 GOSUB 50
33128 GOTO 505


33949 REM CLEAR SCREEN - PRINT LE, ROOM AND DM
33950 PRINT"";
33955 POKE53280,7:POKE53281,7
33960 PRINT CHR$(144)
33965 RL=LEN(RN$(AR,0)):PRINT "";
33970 FOR L =1 TO RL:PRINT"";:NEXT L
33975 PRINT"" TAB(15) "" TAB(30) "  "
33980 PRINT "";RN$(AR,0);"";
33981 PRINT TAB(15) "";RIGHT$(STR$(DM),2);" DM" ;
33982 PRINT TAB(30) "  ";RIGHT$(STR$(LE),1);""
33985 PRINT "";
33990 FOR L =1 TO RL:PRINT"";:NEXT L
33995 PRINT"" TAB(15) "" TAB(30) "  "
33996 PRINT ""
33997 RETURN

34000 REM GG ANPASSEN
34005 GG=0:FOR G=1 TO GS
34010 IF GS%(G,0)=-2 THEN GG=GG+1
34015 NEXT G
34020 RETURN

39999 REM D(1)=MITFAHRER, D(2)=VERKAUEFER , D(3)=FRAU
40000 REM AB HIER DIE TEXTE FUER DAS REDEN....
40001 IF WD$(2)="PAERCHEN"       AND AR=7  THEN GOTO 41000
40002 IF WD$(2)="MITFAHRER"      AND AR=2  THEN GOTO 41020
40003 IF WD$(2)="BESITZER"       AND AR=3  THEN GOTO 41100
40004 IF WD$(2)="VERKAEUFER"     AND AR=9  THEN GOTO 41120
40005 IF WD$(2)="MANN"           AND AR=12 THEN GOTO 41600
40006 IF WD$(2)="FRAU"           AND AR=12 THEN GOTO 41900
40010 IF WD$(2)="" THEN PRINT "ER REDET MIT SICH SELBER..": GOSUB 50:GOTO 505
40011 PRINT "DAMIT KANN ER NICHT (MEHR) REDEN...":GOSUB 50:GOTO 505
41000 GOSUB 33950: REM PAERCHEN AM IMBISS
41001 PRINT "ROCKO SPRICHT DIE BEIDEN AN:'HALLO IHR,"
41002 PRINT "SCHMECKT LECKER, NE .BIN OFT ZUM MITTAG"
41003 PRINT "ZU CURRYWURST UND LIMO HIER.'DIE BEIDEN"
41004 PRINT "SCHAUEN SICH FRAGEND AN,DANN SAGT ER:  "
41005 PRINT "'KENNEN WIR UNS ODER WARUM STOERST DU  "
41006 PRINT "HIER BEIM DATE.' DARAUFHIN DREHEN SIE  "
41007 PRINT "SICH UM UND WIDMEN SICH WIEDER IHREM   "
41008 PRINT "ESSEN.OKAY DENKT ER SICH, WENN DIE MIT "
41009 PRINT "MIR NICHT REDEN WOLLEN DANN EBEN NICHT."
41010 GOSUB 50: GOTO 505
41020 GOSUB 33950: REM MITFAHRER AM BAHNHOF
41021 IF D%(1)=0 GOTO 41030
41022 PRINT "SO SEHR ER SICH AUCH ANSTRENGT, KEINER "
41023 PRINT "DER ANDEREN WILL NOCH MIT IHM REDEN.   "
41024 GOSUB 50: GOTO 505
41030 PRINT "DIE LEUTE SIND ZWAR ALLE IN DEN KLEINEN"
41031 PRINT "GRAUEN KASTEN VERTIEFT ABER ER VERSUCHT"
41032 PRINT "TROTZDEM MIT IHNEN INS GESPRAECH ZU    "
41033 PRINT "KOMMEN.'WAS MACHST DU DENN DA' FRAGT ER"
41034 PRINT "EINEN DER JUENGEREN DIE AM BAHNGLEIS   "
41035 PRINT "STEHEN. DIESER ANTWORTET:'ICH HAB DAS  "
41036 PRINT "NEUE SPIEL - BETRIS- IST GERADE AUF DEM"
41037 PRINT "DADDELKASTEN VON TATARI ERSCHIENEN.MEGA"
41038 PRINT "SPIEL UND DIESE KRASSE 4-FARB GRAFIK!' "
41039 PRINT "ROCKO SCHAUT VERLEGEN,SO EIN TEIL ZUM  "
41040 PRINT "UNTERWEGS SPIELEN HAETTE ER AUCH GERNE."
41041 PRINT "ABER WO SOLL ER DENN SEINEN JOYSTICK  "
41042 PRINT "ANSCHLIESSEN DENKT ER UND DANN WIRD IHM"
41043 PRINT "KLAR - NUR DER C64!                    "
41044 GOSUB 50:GOSUB 33950
41045 PRINT "LEICHT AUFDRINGLICH RUECKT DER JUNGE AN"
41046 PRINT "ROCKO NAHER HERAN.'SOLL ICH DIR EINEN  "
41047 PRINT "CHEATCODE VERRATEN MIT DEM MAN HIER JE-"
41048 PRINT "DEN LEVEL SCHAFFT";
41049 INPUT EG$
41050 IF EG$="JA" THEN 41053
41051 IF EG$="NEIN" THEN 41062
41052 PRINT "WAS DENN NUN. JA ODER NEIN!" : GOTO 41049
41053 PRINT "'GERNE' SAGT ROCKO, WENN AUCH MIT EINEM"
41054 PRINT "LEICHT AENGSTLICHEM GEFUEHL. ABER DER  "
41055 PRINT "JUNGE WILL WIRKLICH NUR SPIELEN UND ER "
41056 PRINT "ZEIGT ROCKO EINEN KRASSEN CHEATCODE.'DU"
41057 PRINT "MUSST HIER NUR HOCH-RUNTER-RECHTS-A-A-B"
41058 PRINT "DRUECKEN UND ZACK - NAECHSTER LEVEL!   "
41059 PRINT "ROCKO MERKT SICH DAS,WER WEISS WANN ER "
41060 PRINT "MAL BRAUCHEN KANN...                   "
41061 D%(1)=1:GOSUB 50:GOTO 505
41062 PRINT "'DANKE, ABER DAS SPIEL MAG ICH NICHT SO"
41063 PRINT "GERNE' SAGT ROCKO UND GEHT DANN SCHNELL"
41064 PRINT "EIN PAAR METER VON DEM JUNGEN WEG.DER  "
41065 PRINT "SCHEINT ETWAS SAUER ZU SEIN UND RUFT   "
41066 PRINT "NOCH 'WIE KANN MAN DAS NICHT MOEGEN! SO"
41067 PRINT "JEMAND HAT EINFACH KEINE AHNUNG VON    "
41068 PRINT "NEUEN SPIELEN.' DANN WIDMET ER SICH    "
41069 PRINT "WIEDER SEINEM DADDELKASTEN ZU. IST WOHL"
41070 PRINT "BESSER DENKT SICH ROCKO...             "
41071 D%(1)=2:GOSUB 50:GOTO 505


41100 GOSUB 33950: REM KIOSKBESITZER
41101 PRINT "'HEY, ICH HOFFE ES GEHT DIR GUT.WAS IST"
41102 PRINT "DENN HEUTE IM ANGEBOT' FRAGT ROCKO DEN "
41103 PRINT "BESITZER. DER ANTWORTET:'ACH ROCKO,DU  "
41104 PRINT "KAUFST DOCH EH IMMER DAS GLEICHE - EINE"
41105 PRINT "LIMO UND EINE FAHRKARTE AM AUTOMATEN.  "
41106 PRINT "ABER VORSICHT-NOCH HAB ICH KEINE NEUE  "
41107 PRINT "LIEFERUNG ERHALTEN. ALSO KAUF LIEBER   "
41108 PRINT "BEVOR SIE ALLE IST. ACH UND DER AUTOMAT"
41109 PRINT "IST LEIDER KAPUTT,TUD MIR LEID DU WIRST"
41110 PRINT "DIR WOANDERS EINE FAHRKARTE BESORGEN   "
41111 PRINT "MUESSEN.' NA TOLL DENKT SICH ROCKO, WO "
41112 PRINT "SOLL ICH DENN DIE NUN HERBEKOMMEN...  "
41113 GOSUB 50: GOTO 505

41120 GOSUB 33950: REM VERKAUEFER
41125 IF D%(2)=1 THEN GOTO 41490
41130 IF D%(2)=2 THEN GOTO 41520

41205 PRINT "'MOIN, DAS IST EIN TOLLES SPIEL IM C64."
41210 PRINT "UND SONST ALLES KLAR BEI DIR HOFFE ICH'"
41215 PRINT "FRAGT ROCKO DEN VERKAUEFER. 'ACH HIER  "
41220 PRINT "IST IMMER WENIGER LOS IN LETZER ZEIT.  "
41225 PRINT "NUN WO DER NEUE DADDELKASTEN VON TATARI"
41230 PRINT "RAUS IST WILL KEINER MEHR DEN C64 KRAM."
41235 PRINT "ABER ICH MUSS DOCH GELD VERDIENEN! NUR "
41240 PRINT "DESWEGEN SIND MEINE SACHEN AUCH IMMER  "
41245 PRINT "SO TEUER.' BEKLAGT SICH DER ALTE MANN. "
41250 PRINT "'WOLLTE JA GERNE EINEN JOYSTICK ABER 20"
41255 PRINT "DM HAB ICH EINFACH NICHT. KANN MAN DA  "
41260 PRINT "NICHTS AM PREIS MACHEN' VERSUCHT ROCKO "
41265 PRINT "ZU VERHANDELN."
41270 GOSUB 50: GOSUB 33949

41275 PRINT "'PUH, EIGENTLICH KANN MAN DA NICHTS AM "
41280 PRINT "PREIS MACHEN. ABER ICH HAB HIER NOCH   "
41285 PRINT "EINEN KAPUTTEN QUICKSHOT IM LAGER. WENN"
41290 PRINT "DU DEN HABEN WILLST - WERDEN WIR UNS   "
41295 PRINT "SICHER EINIG. WOBEI WENN DU MIR HILFST "
41300 PRINT "DIE OLLEN GRAFIKKARTEN ABZUSTAUBEN, GEB"
41305 PRINT "DIR DAS TEIL FUER UMSONST. WAS SAGST DU"
41310 INPUT EG$
41315 IF EG$="JA"   THEN D%(2)=1: GOTO 41330
41320 IF EG$="NEIN" THEN D%(2)=2: GOTO 41460
41325 PRINT "WAS DENN NUN. JA ODER NEIN!" : GOTO 41310
41330 PRINT "'NA KLAR HELF ICH DIR!' SAGT ROCKO UND "
41335 PRINT "DENKT SICH DAS DAS SO VIEL ARBEIT WOHL "
41340 PRINT "KAUM SEIN KANN. DER VERKAEUFER FUEHRT  "
41345 PRINT "ROCKO INS LAGER. ER STEHT VOR EINEM CA."
41350 PRINT "100M LANGEN REGAL.'DAS IST MEIN LAGER. "
41355 PRINT "ICH LASSE HIER 250 VOODOO 3DFX KARTEN  "
41360 PRINT "DIE KOMMENDEN LOTTOZAHLEN AUSRECHNEN.  "
41365 PRINT "ROCKO WIRD SCHWINDELIG ALS ER EINEN    "
41370 PRINT "STAUBWEDEL GEREICHT BEKOMMT.'VIEL SPASS"
41375 PRINT "UND MELDE DICH EINFACH WENN DU FERTIG  "
41380 PRINT "BIST.' SAGT DER VERKAEUFER NOCH UND    "
41385 PRINT "LAESST ROCKO MIT DIESEN WORTEN ALLEIN. "
41390 GOSUB 50: GOSUB 33949
41395 PRINT "EINIGE STUNDEN SPAETER IST ROCKO DANN  "
41400 PRINT "FERTIG UND AUCH EIN WENIG ERSCHOEPFT.  "
41405 PRINT "ABER NUN BLITZEN ALLE KARTEN WIE UND ER"
41410 PRINT "KLOPFT SICH SELBER AB. ZUFRIEDEN GEHT  "
41415 PRINT "ER NACH VORNE IN DEN VERKAUFSRAUM.'AH, "
41420 PRINT "ALLES ERLEDIGT SEHE ICH. SUPER, DANN   "
41425 PRINT "HAST DU DIR DEINE ENTLOHNUNG AUCH WIRK-"
41430 PRINT "LICH VERDIENT.' SAGT DER VERKAEUFER.UND"
41435 PRINT "MIT DIESEN WORTEN STELLT ER IHM DEN    "
41440 PRINT "VERSPROCHENEN QUICKSHOT VOR DIE NASE.  "
41445 PRINT "'ABER NICHT VERGESSEN. DER MUSS NOCH   "
41450 PRINT "REPARIERT WERDEN.                      "
41455 LE=LE-3:GS%(14,1)=2:GOSUB 50: GOTO 505
41460 PRINT "'NA DANN EBEN NICHT. ABER MIT DEM JOY- "
41465 PRINT "STICK WIRD ES DANN HALT AUCH NICHTS.'  "
41470 PRINT "SAGT DER VERKAEUFER MIT LEICHT TRAURIG "
41475 PRINT "KLINGENDER STIMME UND WIDMET SICH DAMIT"
41480 PRINT "WIEDER SEINEN UNTERLAGEN ZU.           "
41485 GOSUB 50: GOTO 505
41490 PRINT "'KANN ICH NOCH WAS FUER DICH TUN ?'    "
41495 PRINT "FRAGT ROCKO DEN MANN. ABER DER WINKT AB"
41500 PRINT "UND SAGT 'DANKE, MEHR HAB ICH GERADE   "
41505 PRINT "NICHT FUER DICH.' UND DAMIT WIDMET ER  "
41510 PRINT "SICH WIEDER SEINEN UNTERLAGEN.         "
41515 GOSUB 50: GOTO 505
41520 PRINT "VIELLEICHT KANN ICH DIR DOCH HELFEN.   "
41525 PRINT "SAGT ROCKO ZUM VERKAUEFER. DIESER SIEHT"
41530 PRINT "IHN MIT GROSSEN AUGEN AN. 'WOHER DER   "
41535 PRINT "SINNESWANDEL KOMMT FRAGE ICH MICH. ABER"
41540 PRINT "EGAL. DANN LEG MAL LOS.' SAGT DIESER   "
41545 PRINT "UND REICHT IHM EINEN STAUBWEDEL.       "
41550 D%(2)=1:GOTO 41390
41600 GOSUB 33950: REM MANN WERKSTATT
41665 PRINT "'MOIN, ICH BIN ROCKO. KANNST DU GUT MIT" 
41670 PRINT "DEM LOETKOLBEN UMGEHEN?' FRAGT ROCKO   "
41675 PRINT "DEN JUNGEN MANN. DER MANN SCHAUT VON   "
41680 PRINT "SEINER BASTELEI AUF UND SAGT: 'ICH KANN"
41685 PRINT "EIGENTLICH SO GUT WIE ALLES REPARIEREN."
41690 PRINT "HAST DU DENN WAS KAPUTTES? ACH UND ICH "
41691 PRINT "HEISSE UEBRIGENS RICK UND DIE LADY HIER"
41692 PRINT "NENNT SICH DIAMOND.'                    "
41695 IF GS%(14,1)=2 AND LEFT$(GS$(14,1),3)="LEI" THEN GOTO 41730
41700 PRINT "'EIGENTLICH HAB ICH NICHTS MEHR WAS ZU "
41705 PRINT "REPARIEREN IST.' SAGT ROCKO NACHDEM ER "
41710 PRINT "SICH SEINE HABSELIGKEITEN ANGESCHAUT   "
41715 PRINT "HAT. UND DAMIT WENDET SICH DER JUNGE   "
41720 PRINT "MANN SOFORT WIEDER DEM LOETKOLBEN ZU.  "
41725 GOSUB 50: GOTO 505
41730 PRINT "'TATSAECHLICH HAB ICH HIER EINEN OLLEN "
41735 PRINT "QUICKSHOT DER KAPUTT IST. VIELLEICHT   "
41740 PRINT "BEKOMMST DU DEN WIEDER HIN?' FRAGT ER  "
41745 PRINT "UND STELLT IHM DEN QUICKSHOT AUF DEN   "
41750 PRINT "TISCH. RICK NIMMT DEN QUICKSHOT IN DIE "
41755 PRINT "HAND, RUETTELT EIN WENIG, DRUECKT DIE  "
41760 PRINT "KNOEPFE UND SAGT DANN: 'DA IST BESTIMMT"
41765 PRINT "NUR EIN KABEL AB. ICH KANN DIR SICHER  "
41770 PRINT "HELFEN, ABER WAS SPRINGT DANN FUER MICH"
41775 PRINT "DABEI RAUS?'                           "
41780 GOSUB 50: GOTO 505 : REM HIER FEHLT NOCH TEXT...

41900 GOSUB 33950: REM FRAU WERKSTATT
41901 IF D%(3)=1 THEN GOTO 42075
41905 PRINT "'HALLO, ICH BIN ROCKO, WAS MACHST DU   "
41910 PRINT "DENN DA?' FRAGT ER DIE JUNGE FRAU. DIE "
41915 PRINT "SCHAUT VON IHREN CHIPS AUF UND LEGT DEN"
41920 PRINT "PINSEL BEISEITE. ' SIEHT MAN DOCH, ICH "
41930 PRINT "BEMALE DIE EPROMS MIT DUNKLER FARBE DA "
41935 PRINT "SIE SONST GELOESCHT WERDEN. EIGENTLICH "
41940 PRINT "WUERDE ICH JA LIEBER MIT MEINEM NEUEN  "
41945 PRINT "SPIEL BETRIS SPIELEN, ABER DA KOMME ICH"
41950 PRINT "AKTUELL NICHT WEITER.'                 "
41951 IF D%(1)=1 THEN GOTO 42000
41955 PRINT "DAS SPIEL MAG ICH LEIDER NICHT."
41960 PRINT "HIER KANN ICH DIR NICHT HELFEN."
41965 PRINT ""
41970 PRINT ""
41975 PRINT ""
41980 GOSUB 50: GOTO 505
42000 PRINT "'DAS SPIEL KENN ICH DOCH!' SPRUDELT ES "
42005 PRINT "NUR SO AUS ROCKO RAUS.'WENN DU WILLST  "
42010 PRINT "KANN ICH DIR EINEN CHEAT CODE VERRATEN "
42015 PRINT "MIT DEM DU WEITER KOMMST.' 'DAS WAERE  "
42020 PRINT "MEGA UND MEIN EWIGER DANK DIR SICHER.' "
42025 PRINT "ANTWORTET SIE FREUDIG UND HOLT IHREN   "
42030 PRINT "DADDELKASTEN VON TATARI AUS DER SCHUB- "
42035 PRINT "LADE UNTER IHREM TISCH HERVOR. ALS SIE "
42040 PRINT "DAS SPIEL EINSCHALTET GIBT SIE ES AN   "
42045 PRINT "ROCKO UND ER TIPPT DEN CHEAT CODE EIN."
42050 PRINT "KAUM HAT SIE DAS GERAET WIEDER IN IHRER"
42055 PRINT "HAND REAGIERT SIE AUF GAR NICHTS MEHR. "
42060 PRINT "'NA GUT, DANN LASS ICH DICH MAL ALLEINE"
42065 PRINT "MIT DEINEM SPIEL' SAGT ER NOCH UND    "
42070 PRINT "BEENDET DAS GESPRAECH.                 "
42071 D%(3)=1 : REM CHEATCODE MITGETEILT
42072 GOSUB 50: GOTO 505
42075 PRINT "'HALLO, UND SCHON WEITER GEKOMMEN?'    "
42076 PRINT "FRAGT ER DIE FRAU. DIESE ABER REAGIERT "
42077 PRINT "AUF KEIN EINZIGES WORT SO VERTIEFT IST "
42078 PRINT "SIE IN IHR SPIEL. AB UND AN HOERT MAN  "
42079 PRINT "NUR EIN 'YEAH, NAECHSTES LEVEL!' VON   "
42080 PRINT "IHR. HIER KANN ROCKO NICHTS MEHR TUN.  "
42081 GOSUB 50: GOTO 505


63000 POKE53280,7:POKE53281,7
63001 ?"";
63002
63003 PRINT"";
63004 PRINT"";
63005 PRINT"";
63006 PRINT"";
63007 PRINT"";
63008 PRINT"";
63009 PRINT"";
63010 PRINT"";
63011 PRINT"";
63012 PRINT"";
63013 PRINT"AELLE KELLERABEND";
63014 PRINT"THE GAME ";
63015 PRINT"         ";
63016 PRINT"              ";
63017 PRINT"VON UND MIT DER";
63018 PRINT" COMMUNITY";
63019 PRINT"   ";
63020 PRINT"            ";
63021 PRINT"";
63022 PRINT"      VON CAPTAIN";
63023 PRINT" RAVER    ";
63024 PRINT" ";
63025 PRINT" ";
63026 PRINT"      FUER UNSEREN ROC";
63027 PRINT"KO      ";
63028 PRINT" ";
63029 PRINT" ";
63030 PRINT"";
63031 PRINT"";
63032 PRINT"";
63033 PRINT"";
63034 PRINT"";
63035 PRINT"";
63036 PRINT""
63037 REM PRINT"";
63038 REM PRINT"";
63039 REM PRINT"";
63040 PRINT"BEFEHLE: GEHE X, NIMM X, SUCHE X"
63041 PRINT"TRINKE X, UNTERSUCHE X, INVENTAR"
63042 REM PRINT"REDE X                      "
63043 PRINT "FREE MEM" : PRINT FRE(0)
63046 PRINT"";
63047 PRINT"";
63048 PRINT"";
63049 PRINT"";
63050 PRINT"";
63051 PRINT"";
63052 PRINT"";
63053 PRINT"";
63054 RETURN